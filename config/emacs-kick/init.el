;;; init.el --- Emacs-Kick --- A feature rich Emacs config for (neo)vi(m)mers -*- lexical-binding: t; -*-

;; Package-Requires: ((emacs "30.1"))

;;; Code:

;;; STARTUP
(setq gc-cons-threshold #x40000000)

(setq read-process-output-max (* 1024 1024 4))

(setq native-comp-jit-compilation nil)

;;; PACKAGE BOOTSTRAP
(setq package-enable-at-startup nil)

(defvar straight-check-for-modifications)
(setq straight-check-for-modifications nil)
(defvar bootstrap-version)
(let ((bootstrap-file
       (expand-file-name
        "straight/repos/straight.el/bootstrap.el"
        (or (bound-and-true-p straight-base-dir)
            user-emacs-directory)))
      (bootstrap-version 7))
  (unless (file-exists-p bootstrap-file)
    (with-current-buffer
        (url-retrieve-synchronously
         "https://raw.githubusercontent.com/radian-software/straight.el/develop/install.el"
         'silent 'inhibit-cookies)
      (goto-char (point-max))
      (eval-print-last-sexp)))
  (load bootstrap-file nil 'nomessage))
(straight-use-package '(project :type built-in))
(straight-use-package 'use-package)


(require 'package)

(add-to-list 'package-archives '("melpa" . "https://melpa.org/packages/") t)

(defcustom ek-use-nerd-fonts t
  "Configuration for using Nerd Fonts Symbols."
  :type 'boolean
  :group 'appearance)


;;; EMACS
(use-package emacs
  :ensure nil
  :custom                                         ;; Set custom variables to configure Emacs behavior.
  (auto-save-default nil)                         ;; Disable automatic saving of buffers.
  (column-number-mode t)                          ;; Display the column number in the mode line.
  (create-lockfiles nil)                          ;; Prevent the creation of lock files when editing.
  (delete-by-moving-to-trash t)                   ;; Move deleted files to the trash instead of permanently deleting them.
  (delete-selection-mode 1)                       ;; Enable replacing selected text with typed text.
  (display-line-numbers-type 'relative)           ;; Use relative line numbering in programming modes.
  (global-auto-revert-non-file-buffers t)         ;; Automatically refresh non-file buffers.
  (history-length 25)                             ;; Set the length of the command history.
  (indent-tabs-mode nil)                          ;; Disable the use of tabs for indentation (use spaces instead).
  (inhibit-startup-message t)                     ;; Disable the startup message when Emacs launches.
  (initial-scratch-message "")                    ;; Clear the initial message in the *scratch* buffer.
  (ispell-dictionary "en_US")                     ;; Set the default dictionary for spell checking.
  (make-backup-files nil)                         ;; Disable creation of backup files.
  (pixel-scroll-precision-mode t)                 ;; Enable precise pixel scrolling.
  (pixel-scroll-precision-use-momentum nil)       ;; Disable momentum scrolling for pixel precision.
  (ring-bell-function 'ignore)                    ;; Disable the audible bell.
  (split-width-threshold 300)                     ;; Prevent automatic window splitting if the window width exceeds 300 pixels.
  (switch-to-buffer-obey-display-actions t)       ;; Make buffer switching respect display actions.
  (tab-always-indent 'complete)                   ;; Make the TAB key complete text instead of just indenting.
  (tab-width 4)                                   ;; Set the tab width to 4 spaces.
  (treesit-font-lock-level 4)                     ;; Use advanced font locking for Treesit mode.
  (truncate-lines t)                              ;; Enable line truncation to avoid wrapping long lines.
  (use-dialog-box nil)                            ;; Disable dialog boxes in favor of minibuffer prompts.
  (use-short-answers t)                           ;; Use short answers in prompts for quicker responses (y instead of yes)
  (warning-minimum-level :emergency)              ;; Set the minimum level of warnings to display.

  :hook                                           ;; Add hooks to enable specific features in certain modes.
  (prog-mode . display-line-numbers-mode)         ;; Enable line numbers in programming modes.

  :config
  ;; By default emacs gives you access to a lot of *special* buffers, while navigating with [b and ]b,
  ;; this might be confusing for newcomers. This settings make sure ]b and [b will always load a
  ;; file buffer. To see all buffers use <leader> SPC, <leader> b l, or <leader> b i.
  (defun ek-ai-code-buffer-p (buffer-or-name)
    "Return non-nil when BUFFER-OR-NAME is an AI Code session or helper buffer."
    (let ((name (if (stringp buffer-or-name)
                    buffer-or-name
                  (buffer-name buffer-or-name))))
      (or (string-match-p "\\`\\*.*\\[.*\\].*\\*\\'" name)
          (string-match-p "\\`\\*[Aa][Ii] Code.*\\*\\'" name)
          (string-match-p "\\`\\*ai-code-.*\\*\\'" name)
          (string-match-p "\\`ai-code-mcp-http-server <.*>\\'" name))))

  (defun ek-magit-buffer-p (buffer-or-name)
    "Return non-nil when BUFFER-OR-NAME is a Magit buffer."
    (let ((buffer (if (bufferp buffer-or-name)
                      buffer-or-name
                    (get-buffer buffer-or-name))))
      (and buffer
           (with-current-buffer buffer
             (derived-mode-p 'magit-mode 'magit-process-mode)))))

  (defun skip-these-buffers (_window buffer _bury-or-kill)
    "Function for `switch-to-prev-buffer-skip'."
    (or (string-match "\\*[^*]+\\*" (buffer-name buffer))
        (ek-ai-code-buffer-p buffer)
        (ek-magit-buffer-p buffer)))
  (setq switch-to-prev-buffer-skip 'skip-these-buffers)
  (with-eval-after-load 'consult
    (dolist (regexp '("\\`\\*.*\\[.*\\].*\\*\\'"
                      "\\`\\*[Aa][Ii] Code.*\\*\\'"
                      "\\`\\*ai-code-.*\\*\\'"
                      "\\`ai-code-mcp-http-server <.*>\\'"
                      "\\`magit\\(?::.*\\)?\\'"
                      "\\`\\*magit.*\\*\\'"
                      "\\`\\*.*magit.*\\*\\'"))
      (add-to-list 'consult-buffer-filter regexp)))


  ;; Configure font settings based on the operating system.
  ;; Ok, this kickstart is meant to be used on the terminal, not on GUI.
  ;; But without this, I fear you could start Graphical Emacs and be sad :(
  (set-face-attribute 'default nil :family "JetBrainsMono Nerd Font"  :height 150)
  (when (eq system-type 'darwin)       ;; Check if the system is macOS.
    ;; Keep Command as Meta and leave Option free for window-manager bindings.
    (setq mac-command-modifier 'meta
          mac-option-key-is-meta nil
          mac-option-modifier 'none
          mac-right-option-modifier 'none
          mac-pass-option-to-system nil
          ns-command-modifier 'meta
          ns-option-key-is-meta nil
          ns-option-modifier 'none)  ;; Set the Command key to act as the Meta key.
    (set-face-attribute 'default nil :family "JetBrainsMono Nerd Font" :height 180))

  ;; Save manual customizations to a separate file instead of cluttering `init.el'.
  ;; You can M-x customize, M-x customize-group, or M-x customize-themes, etc.
  ;; The saves you do manually using the Emacs interface would overwrite this file.
  ;; The following makes sure those customizations are in a separate file.
  (setq custom-file (locate-user-emacs-file "custom-vars.el")) ;; Specify the custom file path.
  (load custom-file 'noerror 'nomessage)                       ;; Load the custom file quietly, ignoring errors.

  ;; Makes Emacs vertical divisor the symbol │ instead of |.
  (set-display-table-slot standard-display-table 'vertical-border (make-glyph-code ?│))

  :init                        ;; Initialization settings that apply before the package is loaded.
  (tool-bar-mode -1)           ;; Disable the tool bar for a cleaner interface.
  (menu-bar-mode -1)           ;; Disable the menu bar for a more streamlined look.

  (when scroll-bar-mode
    (scroll-bar-mode -1))      ;; Disable the scroll bar if it is active.

  (when (eq system-type 'darwin)
    ;; Keep the GUI frame free of macOS title-bar chrome.
    (add-to-list 'default-frame-alist '(undecorated-round . t)))

  (global-hl-line-mode -1)     ;; Disable highlight of the current line
  (global-auto-revert-mode 1)  ;; Enable global auto-revert mode to keep buffers up to date with their corresponding files.
  (recentf-mode 1)             ;; Enable tracking of recently opened files.
  (savehist-mode 1)            ;; Enable saving of command history.
  (save-place-mode 1)          ;; Enable saving the place in files for easier return.
  (winner-mode 1)              ;; Enable winner mode to easily undo window configuration changes.
  (xterm-mouse-mode 1)         ;; Enable mouse support in terminal mode.
  (file-name-shadow-mode 1)    ;; Enable shadowing of filenames for clarity.

  ;; Set the default coding system for files to UTF-8.
  (modify-coding-system-alist 'file "" 'utf-8)

  ;; Add a hook to run code after Emacs has fully initialized.
  (add-hook 'after-init-hook
            (lambda ()
              (message "Emacs has fully loaded. This code runs after startup.")

              ;; Insert a welcome message in the *scratch* buffer displaying loading time and activated packages.
              (with-current-buffer (get-buffer-create "*scratch*")
                (insert (format
                         ";;    Welcome to Emacs!
;;
;;    Loading time : %s
;;    Packages     : %s
"
                         (emacs-init-time)
                         (length (hash-table-keys straight--recipe-cache))))))))


;;; WINDOW
(defun ek/split-window-below-and-focus ()
  "Split the current window below and move focus to the new window."
  (interactive)
  (split-window-below)
  (other-window 1))

(defun ek/split-window-right-and-focus ()
  "Split the current window right and move focus to the new window."
  (interactive)
  (split-window-right)
  (other-window 1))

(defconst ek/window-resize-step 5
  "Number of columns/lines to resize windows by per command.")

(defun ek/window-resize-left ()
  "Shrink the current window horizontally."
  (interactive)
  (shrink-window-horizontally ek/window-resize-step))

(defun ek/window-resize-right ()
  "Enlarge the current window horizontally."
  (interactive)
  (enlarge-window-horizontally ek/window-resize-step))

(defun ek/window-resize-down ()
  "Shrink the current window vertically."
  (interactive)
  (shrink-window ek/window-resize-step))

(defun ek/window-resize-up ()
  "Enlarge the current window vertically."
  (interactive)
  (enlarge-window ek/window-resize-step))

(defun ek/ai-code-window-width-columns ()
  "Return the desired AI side-window width in columns."
  (max 40 (floor (* (frame-width) 0.45))))

(defun ek/update-ai-code-window-width (&rest _)
  "Keep the AI side-window width at two-fifths of the current frame."
  (setq ai-code-backends-infra-window-width
        (ek/ai-code-window-width-columns)))

(defun ek/tab-name-for-project-root (root)
  "Return a human-friendly tab name for project ROOT."
  (file-name-nondirectory (directory-file-name root)))

(defun ek/tab-index-by-name (name)
  "Return the 1-based tab index for tab NAME, or nil when absent."
  (let ((index 1)
        match)
    (dolist (tab (tab-bar-tabs))
      (when (equal (alist-get 'name tab) name)
        (setq match index))
      (setq index (1+ index)))
    match))

(defun ek/switch-or-create-project-workspace (dir)
  "Switch to or create a tab workspace for project DIR."
  (interactive "DProject directory: ")
  (let* ((root (file-name-as-directory (expand-file-name dir)))
         (tab-name (ek/tab-name-for-project-root root))
         (existing (ek/tab-index-by-name tab-name)))
    (if existing
        (tab-bar-select-tab existing)
      (tab-bar-new-tab)
      (tab-bar-rename-tab tab-name))
    (let ((default-directory root))
      (dired root))))

(defun ek/project-switch-project-in-workspace ()
  "Open the selected project in its own tab workspace."
  (interactive)
  (let ((dir (project-prompt-project-dir)))
    (let ((default-directory dir))
      (when-let ((project (project-current nil)))
        (project-remember-project project)))
    (ek/switch-or-create-project-workspace dir)))

(use-package tab-bar
  :ensure nil
  :custom
  (tab-bar-close-button-show nil)
  (tab-bar-new-button-show nil)
  (tab-bar-tab-hints t)
  (tab-bar-show 0)
  :init
  (tab-bar-mode 1))

(use-package window
  :ensure nil       ;; This is built-in, no need to fetch it.
  :custom
  (display-buffer-alist
   '(
     ;; ("\\*.*e?shell\\*"
     ;;  (display-buffer-in-side-window)
     ;;  (window-height . 0.25)
     ;;  (side . bottom)
     ;;  (slot . -1))

     ("\\*\\(Backtrace\\|Warnings\\|Compile-Log\\|[Hh]elp\\|Messages\\|Bookmark List\\|Ibuffer\\|Occur\\|eldoc.*\\)\\*"
      (display-buffer-in-side-window)
      (window-height . 0.25)
      (side . bottom)
      (slot . 0))

     ;; Example configuration for the LSP help buffer,
     ;; keeps it always on bottom using 25% of the available space:
     ("\\*\\(lsp-help\\)\\*"
      (display-buffer-in-side-window)
      (window-height . 0.25)
      (side . bottom)
      (slot . 0))

     ;; Configuration for displaying various diagnostic buffers on
     ;; bottom 25%:
     ("\\*\\(Flymake diagnostics\\|xref\\|ivy\\|Swiper\\|Completions\\)"
      (display-buffer-in-side-window)
      (window-height . 0.25)
      (side . bottom)
      (slot . 1))
     )))


;;; DIRED

(use-package dired
  :ensure nil                                                ;; This is built-in, no need to fetch it.
  :custom
  (dired-listing-switches "-lah --group-directories-first")  ;; Display files in a human-readable format and group directories first.
  (dired-dwim-target t)                                      ;; Enable "do what I mean" for target directories.
  (dired-guess-shell-alist-user
   '(("\\.\\(png\\|jpe?g\\|tiff\\)" "feh" "xdg-open" "open") ;; Open image files with `feh' or the default viewer.
     ("\\.\\(mp[34]\\|m4a\\|ogg\\|flac\\|webm\\|mkv\\)" "mpv" "xdg-open" "open") ;; Open audio and video files with `mpv'.
     (".*" "open" "xdg-open")))                              ;; Default opening command for other files.
  (dired-kill-when-opening-new-dired-buffer t)               ;; Close the previous buffer when opening a new `dired' instance.
  :config
  (with-eval-after-load 'evil-collection-dired
    (define-key (evil-get-auxiliary-keymap dired-mode-map 'normal t)
                (kbd "SPC")
                (lookup-key evil-normal-state-map (kbd "SPC"))))
  (when (eq system-type 'darwin)
    (let ((gls (executable-find "gls")))                     ;; Use GNU ls on macOS if available.
      (when gls
        (setq insert-directory-program gls)))))


;;; ERC
(use-package erc
  :defer t ;; Load ERC when needed rather than at startup. (Load it with `M-x erc RET')
  :custom
  (erc-join-buffer 'window)                                        ;; Open a new window for joining channels.
  (erc-hide-list '("JOIN" "PART" "QUIT"))                          ;; Hide messages for joins, parts, and quits to reduce clutter.
  (erc-timestamp-format "[%H:%M]")                                 ;; Format for timestamps in messages.
  (erc-autojoin-channels-alist '((".*\\.libera\\.chat" "#emacs"))));; Automatically join the #emacs channel on Libera.Chat.


;;; ISEARCH
(use-package isearch
  :ensure nil                                  ;; This is built-in, no need to fetch it.
  :config
  (setq isearch-lazy-count t)                  ;; Enable lazy counting to show current match information.
  (setq lazy-count-prefix-format "(%s/%s) ")   ;; Format for displaying current match count.
  (setq lazy-count-suffix-format nil)          ;; Disable suffix formatting for match count.
  (setq search-whitespace-regexp ".*?")        ;; Allow searching across whitespace.
  :bind (("C-s" . isearch-forward)             ;; Bind C-s to forward isearch.
         ("C-r" . isearch-backward)))          ;; Bind C-r to backward isearch.


;;; VC
(use-package vc
  :ensure nil                        ;; This is built-in, no need to fetch it.
  :defer t
  :bind
  (("C-x v d" . vc-dir)              ;; Open VC directory for version control status.
   ("C-x v =" . vc-diff)             ;; Show differences for the current file.
   ("C-x v D" . vc-root-diff)        ;; Show differences for the entire repository.
   ("C-x v v" . vc-next-action))     ;; Perform the next version control action.
  :config
  ;; Better colors for <leader> g b  (blame file)
  (setq vc-annotate-color-map
        '((20 . "#f5e0dc")
          (40 . "#f2cdcd")
          (60 . "#f5c2e7")
          (80 . "#cba6f7")
          (100 . "#f38ba8")
          (120 . "#eba0ac")
          (140 . "#fab387")
          (160 . "#f9e2af")
          (180 . "#a6e3a1")
          (200 . "#94e2d5")
          (220 . "#89dceb")
          (240 . "#74c7ec")
          (260 . "#89b4fa")
          (280 . "#b4befe"))))


;;; SMERGE
(use-package smerge-mode
  :ensure nil                                  ;; This is built-in, no need to fetch it.
  :defer t
  :bind (:map smerge-mode-map
              ("C-c ^ u" . smerge-keep-upper)  ;; Keep the changes from the upper version.
              ("C-c ^ l" . smerge-keep-lower)  ;; Keep the changes from the lower version.
              ("C-c ^ n" . smerge-next)        ;; Move to the next conflict.
              ("C-c ^ p" . smerge-previous)))  ;; Move to the previous conflict.


;;; ELDOC
(use-package eldoc
  :ensure nil                                ;; This is built-in, no need to fetch it.
  :config
  (setq eldoc-idle-delay 0)                  ;; Automatically fetch doc help
  (setq eldoc-echo-area-use-multiline-p nil) ;; We use the "K" floating help instead
                                             ;; set to t if you want docs on the echo area
  (setq eldoc-echo-area-display-truncation-message nil)
  :init
  (global-eldoc-mode))


;;; FLYMAKE
(use-package flymake
  :ensure nil          ;; This is built-in, no need to fetch it.
  :defer t
  :hook (prog-mode . flymake-mode)
  :custom
  (flymake-margin-indicators-string
   '((error "!»" compilation-error) (warning "»" compilation-warning)
     (note "»" compilation-info))))


;;; ORG-MODE
(use-package org
  :ensure nil     ;; This is built-in, no need to fetch it.
  :defer t
  :custom
  (org-directory (expand-file-name "~/Documents/org/"))
  (org-agenda-files
   (mapcar #'expand-file-name
           '("~/Documents/org/inbox.org"
             "~/Documents/org/tasks.org"
             "~/Documents/org/todo.org")))
  :config
  (setq-default fill-column 80))

(use-package org-modern
  :ensure t
  :straight t
  :after org
  :hook
  (org-mode . org-modern-mode))

(use-package org-roam
  :ensure t
  :straight t
  :defer t
  :init
  (setq org-roam-directory (file-truename "~/Documents/org/roam"))
  :config
  (org-roam-db-autosync-mode 1))


;;; WHICH-KEY
(use-package which-key
  :ensure nil     ;; This is built-in, no need to fetch it.
  :defer t        ;; Defer loading Which-Key until after init.
  :hook
  (after-init . which-key-mode)) ;; Enable which-key mode after initialization.


;;; IBUFFER
(use-package ibuffer
  :ensure nil
  :defer t
  :init
  (require 'ibuf-ext)
  (add-to-list 'ibuffer-never-show-predicates #'ek-ai-code-buffer-p))


;;; ==================== EXTERNAL PACKAGES ====================
;;

;;; VERTICO
(use-package vertico
  :ensure t
  :straight t
  :hook
  (after-init . vertico-mode)           ;; Enable vertico after Emacs has initialized.
  :custom
  (vertico-count 10)                    ;; Number of candidates to display in the completion list.
  (vertico-resize nil)                  ;; Disable resizing of the vertico minibuffer.
  (vertico-cycle nil)                   ;; Do not cycle through candidates when reaching the end of the list.
  :config
  ;; Customize the display of the current candidate in the completion list.
  ;; This will prefix the current candidate with “» ” to make it stand out.
  ;; Reference: https://github.com/minad/vertico/wiki#prefix-current-candidate-with-arrow
  (advice-add #'vertico--format-candidate :around
              (lambda (orig cand prefix suffix index _start)
                (setq cand (funcall orig cand prefix suffix index _start))
                (concat
                 (if (= vertico--index index)
                     (propertize "» " 'face '(:foreground "#80adf0" :weight bold))
                   "  ")
                 cand))))


;;; ORDERLESS
(use-package orderless
  :ensure t
  :straight t
  :defer t                                    ;; Load Orderless on demand.
  :after vertico                              ;; Ensure Vertico is loaded before Orderless.
  :init
  (setq completion-styles '(orderless basic)  ;; Set the completion styles.
        completion-category-defaults nil      ;; Clear default category settings.
        completion-category-overrides '((file (styles partial-completion))))) ;; Customize file completion styles.


;;; MARGINALIA
(use-package marginalia
  :ensure t
  :straight t
  :hook
  (after-init . marginalia-mode))


;;; CONSULT
(use-package consult
  :ensure t
  :straight t
  :defer t
  :init
  ;; Enhance register preview with thin lines and no mode line.
  (advice-add #'register-preview :override #'consult-register-window)

  ;; Use Consult for xref locations with a preview feature.
  (setq xref-show-xrefs-function #'consult-xref
        xref-show-definitions-function #'consult-xref))


;;; EMBARK
(use-package embark
  :ensure t
  :straight t
  :defer t)


;;; EMBARK-CONSULT
(use-package embark-consult
  :ensure t
  :straight t
  :hook
  (embark-collect-mode . consult-preview-at-point-mode)) ;; Enable preview in Embark collect mode.


;;; TREESITTER-AUTO
(use-package treesit-auto
  :ensure t
  :straight t
  :after emacs
  :custom
  (treesit-auto-install 'prompt)
  :config
  (treesit-auto-add-to-auto-mode-alist 'all)
  (global-treesit-auto-mode t))


;;; MARKDOWN-MODE
(use-package markdown-mode
  :defer t
  :straight t
  :ensure t
  :mode ("README\\.md\\'" . gfm-mode)            ;; Use gfm-mode for README.md files.
  :init (setq markdown-command "multimarkdown")) ;; Set the Markdown processing command.


;;; CORFU
(use-package corfu
  :ensure t
  :straight t
  :defer t
  :custom
  (corfu-auto nil)                        ;; Only completes when hitting TAB
  ;; (corfu-auto-delay 0)                ;; Delay before popup (enable if corfu-auto is t)
  (corfu-auto-prefix 1)                  ;; Trigger completion after typing 1 character
  (corfu-quit-no-match t)                ;; Quit popup if no match
  (corfu-scroll-margin 5)                ;; Margin when scrolling completions
  (corfu-max-width 50)                   ;; Maximum width of completion popup
  (corfu-min-width 50)                   ;; Minimum width of completion popup
  (corfu-popupinfo-delay 0.5)            ;; Delay before showing documentation popup
  :config
  (if ek-use-nerd-fonts
    (add-to-list 'corfu-margin-formatters #'nerd-icons-corfu-formatter))
  :init
  (global-corfu-mode)
  (corfu-popupinfo-mode t))


;;; NERD-ICONS-CORFU
(use-package nerd-icons-corfu
  :if ek-use-nerd-fonts
  :ensure t
  :straight t
  :defer t
  :after (:all corfu))


;;; LSP
(use-package lsp-mode
  :ensure t
  :straight t
  :defer t
  :hook (;; Replace XXX-mode with concrete major mode (e.g. python-mode)
         (lsp-mode . lsp-enable-which-key-integration)  ;; Integrate with Which Key
         ((js-mode                                      ;; Enable LSP for JavaScript
           tsx-ts-mode                                  ;; Enable LSP for TSX
           typescript-ts-base-mode                      ;; Enable LSP for TypeScript
           css-mode                                     ;; Enable LSP for CSS
           go-ts-mode                                   ;; Enable LSP for Go
           js-ts-mode                                   ;; Enable LSP for JavaScript (TS mode)
           prisma-mode                                  ;; Enable LSP for Prisma
           python-base-mode                             ;; Enable LSP for Python
           ruby-base-mode                               ;; Enable LSP for Ruby
           rust-ts-mode                                 ;; Enable LSP for Rust
           web-mode) . lsp-deferred))                   ;; Enable LSP for Web (HTML)
  :commands lsp
  :custom
  (lsp-keymap-prefix "C-c l")                           ;; Set the prefix for LSP commands.
  (lsp-inlay-hint-enable nil)                           ;; Usage of inlay hints.
  (lsp-completion-provider :none)                       ;; Disable the default completion provider.
  (lsp-session-file (locate-user-emacs-file ".lsp-session")) ;; Specify session file location.
  (lsp-log-io nil)                                      ;; Disable IO logging for speed.
  (lsp-idle-delay 1.0)                                  ;; Give the buffer time to settle before LSP chatter.
  (lsp-keep-workspace-alive nil)                        ;; Disable keeping the workspace alive.
  ;; Core settings
  (lsp-enable-xref t)                                   ;; Enable cross-references.
  (lsp-auto-configure t)                                ;; Automatically configure LSP.
  (lsp-enable-links nil)                                ;; Disable links.
  (lsp-eldoc-enable-hover t)                            ;; Enable ElDoc hover.
  (lsp-enable-file-watchers nil)                        ;; Disable file watchers.
  (lsp-enable-folding nil)                              ;; Disable folding.
  (lsp-enable-imenu t)                                  ;; Enable Imenu support.
  (lsp-enable-indentation nil)                          ;; Disable indentation.
  (lsp-enable-on-type-formatting nil)                   ;; Disable on-type formatting.
  (lsp-enable-suggest-server-download t)                ;; Enable server download suggestion.
  (lsp-enable-symbol-highlighting nil)                  ;; Disable symbol repainting on cursor movement.
  (lsp-enable-text-document-color nil)                  ;; Disable inline color scanning.
  ;; Modeline settings
  (lsp-modeline-code-actions-enable nil)                ;; Keep modeline clean.
  (lsp-modeline-diagnostics-enable nil)                 ;; Use `flymake' instead.
  (lsp-modeline-workspace-status-enable nil)            ;; Avoid extra modeline updates.
  (lsp-signature-doc-lines 1)                           ;; Limit echo area to one line.
  (lsp-eldoc-render-all t)                              ;; Render all ElDoc messages.
  ;; Completion settings
  (lsp-completion-enable t)                             ;; Enable completion.
  (lsp-completion-enable-additional-text-edit t)        ;; Enable additional text edits for completions.
  (lsp-enable-snippet nil)                              ;; Disable snippets
  (lsp-completion-show-kind t)                          ;; Show kind in completions.
  ;; Lens settings
  (lsp-lens-enable nil)                                 ;; Lenses add noticeable buffer churn.
  ;; Headerline settings
  (lsp-headerline-breadcrumb-enable nil)                ;; Breadcrumbs are useful, but expensive in large buffers.
  (lsp-headerline-breadcrumb-enable-symbol-numbers t)   ;; Enable symbol numbers in the headerline.
  (lsp-headerline-arrow "▶")                            ;; Set arrow for headerline.
  (lsp-headerline-breadcrumb-enable-diagnostics nil)    ;; Disable diagnostics in headerline.
  (lsp-headerline-breadcrumb-icons-enable nil)          ;; Disable icons in breadcrumb.
  ;; Semantic settings
  (lsp-semantic-tokens-enable nil))                     ;; Disable semantic tokens.


;;; LSP Pyright
(use-package lsp-pyright
  :ensure t
  :straight t
  :after lsp-mode
  :hook
  (python-base-mode . (lambda () (require 'lsp-pyright)))
  :custom
  (lsp-pyright-langserver-command "basedpyright")
  (lsp-pyright-typechecking-mode "basic"))


;;; LSP Additional Servers
(use-package lsp-tailwindcss
  :ensure t
  :straight t
  :defer t
  :config
  (add-to-list 'lsp-language-id-configuration '(".*\\.erb$" . "html")) ;; Associate ERB files with HTML.
  :init
  (setq lsp-tailwindcss-add-on-mode t))


;;; ELDOC-BOX
(use-package eldoc-box
  :ensure t
  :straight t
  :defer t)


;;; DIFF-HL
(use-package diff-hl
  :defer t
  :straight t
  :ensure t
  :hook
  (after-init . global-diff-hl-mode)
  (diff-hl-mode . diff-hl-flydiff-mode)
  (diff-hl-mode . diff-hl-margin-mode)
  :custom
  (diff-hl-side 'left)                           ;; Set the side for diff indicators.
  (diff-hl-margin-symbols-alist '((insert . "┃") ;; Customize symbols for each change type.
                                  (delete . "-")
                                  (change . "┃")
                                  (unknown . "┆")
                                  (ignored . "i"))))


;;; MAGIT
(use-package magit
  :ensure t
  :straight t
  :config
  (if ek-use-nerd-fonts   ;; Check if nerd fonts are being used
	  (setopt magit-format-file-function #'magit-format-file-nerd-icons)) ;; Turns on magit nerd-icons
  :defer t)


;;; VTERM
(use-package vterm
  :ensure t
  :straight t
  :commands (vterm vterm-mode)
  :init
  (defvar ek/vterm-popup-buffer-name "*vterm-popup*"
    "Buffer name used for the popup terminal.")
  (defvar ek/vterm-full-buffer-name "*vterm*"
    "Buffer name used for the full-size terminal.")

  (defun ek/vterm--get-or-create-buffer (buffer-name)
    "Return a live vterm buffer named BUFFER-NAME, creating it if needed."
    (or (when-let ((buffer (get-buffer buffer-name)))
          (when (buffer-live-p buffer)
            buffer))
        (save-window-excursion
          (vterm buffer-name)
          (current-buffer))))

  (defun ek/vterm-toggle-popup ()
    "Toggle a popup vterm in a bottom side window."
    (interactive)
    (let* ((buffer (ek/vterm--get-or-create-buffer ek/vterm-popup-buffer-name))
           (window (get-buffer-window buffer t)))
      (if (window-live-p window)
          (delete-window window)
        (let ((display-buffer-overriding-action
               '((display-buffer-in-side-window)
                 (side . bottom)
                 (slot . -1)
                 (window-height . 0.3))))
          (pop-to-buffer buffer)
          (select-window (get-buffer-window buffer t))))))

  (defun ek/vterm-open-full ()
    "Open a dedicated vterm buffer and make it fill the current frame."
    (interactive)
    (let ((buffer (ek/vterm--get-or-create-buffer ek/vterm-full-buffer-name)))
      (switch-to-buffer buffer)
      (delete-other-windows)))
  :defer t)


;;; EAT
(use-package eat
  :ensure t
  :straight t
  :commands (eat eat-mode)
  :config
  (with-eval-after-load 'evil-collection
    (evil-collection-define-key 'normal 'eat-mode-map
      "p" #'eat-yank
      "P" #'eat-yank))
  :defer t)


;;; AI CODE
(use-package ai-code
  :ensure t
  :straight t
  :defer t
  :config
  (setq ai-code-backends-infra-window-side 'right)
  (ek/update-ai-code-window-width)
  (add-hook 'window-size-change-functions #'ek/update-ai-code-window-width)
  (setq ai-code-backends-infra-terminal-backend 'eat)
  (ai-code-set-backend 'codex)
  (setq ai-code-auto-test-type 'ask-me)
  (ai-code-prompt-filepath-completion-mode 1))


;;; XCLIP
(use-package xclip
  :ensure t
  :straight t
  :defer t
  :hook
  (after-init . xclip-mode))     ;; Enable xclip mode after initialization.


;;; INDENT-GUIDE
(use-package indent-guide
  :defer t
  :straight t
  :ensure t
  :hook
  (prog-mode . indent-guide-mode)  ;; Activate indent-guide in programming modes.
  :config
  (setq indent-guide-char "│"))    ;; Set the character used for the indent guide.


;;; ADD-NODE-MODULES-PATH
(use-package add-node-modules-path
  :ensure t
  :straight t
  :defer t
  :custom
  ;; Makes sure you are using the local bin for your
  ;; node project. Local eslint, typescript server...
  (eval-after-load 'typescript-ts-mode
    '(add-hook 'typescript-ts-mode-hook #'add-node-modules-path))
  (eval-after-load 'tsx-ts-mode
    '(add-hook 'tsx-ts-mode-hook #'add-node-modules-path))
  (eval-after-load 'typescriptreact-mode
    '(add-hook 'typescriptreact-mode-hook #'add-node-modules-path))
  (eval-after-load 'js-mode
    '(add-hook 'js-mode-hook #'add-node-modules-path)))


;;; EVIL
(use-package evil
  :ensure t
  :straight t
  :defer t
  :hook
  (after-init . evil-mode)
  :init
  (setq evil-want-integration t)      ;; Integrate `evil' with other Emacs features (optional as it's true by default).
  (setq evil-want-keybinding nil)     ;; Disable default keybinding to set custom ones.
  (setq evil-want-C-u-scroll t)       ;; Makes C-u scroll
  (setq evil-want-C-u-delete t)       ;; Makes C-u delete on insert mode
  :config
  (evil-set-undo-system 'undo-tree)   ;; Uses the undo-tree package as the default undo system

  (setq evil-want-fine-undo t)        ;; Evil uses finer grain undoing steps
  (evil-define-key 'normal 'global (kbd "] d") 'flymake-goto-next-error) ;; Go to next Flymake error
  (evil-define-key 'normal 'global (kbd "[ d") 'flymake-goto-prev-error) ;; Go to previous Flymake error

  ;; Diff-HL navigation for version control
  (evil-define-key 'normal 'global (kbd "] c") 'diff-hl-next-hunk) ;; Next diff hunk
  (evil-define-key 'normal 'global (kbd "[ c") 'diff-hl-previous-hunk) ;; Previous diff hunk

  ;; Yank from kill ring
  (evil-define-key 'normal 'global (kbd "P") 'consult-yank-from-kill-ring)

  ;; LSP commands keybindings
  (evil-define-key 'normal lsp-mode-map
                   ;; (kbd "gd") 'lsp-find-definition                ;; evil-collection already provides gd
                   (kbd "gr") 'lsp-find-references                   ;; Finds LSP references
                   (kbd "gI") 'lsp-find-implementation)              ;; Find implementation


  (defun ek/lsp-describe-and-jump ()
    "Show hover documentation and jump to *lsp-help* buffer."
    (interactive)
    (lsp-describe-thing-at-point)
    (let ((help-buffer "*lsp-help*"))
      (when (get-buffer help-buffer)
        (switch-to-buffer-other-window help-buffer))))

  ;; Emacs 31 finaly brings us support for 'floating windows' (a.k.a. "child frames")
  ;; to terminal Emacs. If you're still using 30, docs will be shown in a buffer at the
  ;; inferior part of your frame.
  (evil-define-key 'normal 'global (kbd "K")
    (if (>= emacs-major-version 31)
        #'eldoc-box-help-at-point
        #'ek/lsp-describe-and-jump))

  ;; Commenting functionality for single and multiple lines
  (evil-define-key 'normal 'global (kbd "gcc")
                   (lambda ()
                     (interactive)
                     (if (not (use-region-p))
                         (comment-or-uncomment-region (line-beginning-position) (line-end-position)))))

  (evil-define-key 'visual 'global (kbd "gc")
                   (lambda ()
                     (interactive)
                     (if (use-region-p)
                         (comment-or-uncomment-region (region-beginning) (region-end)))))

  ;; Enable evil mode
  (evil-mode 1))


;;; GENERAL
(use-package general
  :ensure t
  :straight t
  :after evil
  :config
  (general-create-definer ek/leader
    :states '(normal visual motion)
    :keymaps 'override
    :prefix "SPC"
    :global-prefix "M-SPC")

  (ek/leader
    "/" 'consult-line
    "." 'embark-act
    "SPC" 'consult-buffer
    "P" 'consult-yank-from-kill-ring
    "u" 'undo-tree-visualize

    "a a" 'ai-code-menu
    "a s" 'ai-code-cli-start
    "a z" 'ai-code-cli-switch-to-buffer-or-hide
    "a q" 'ai-code-ask-question
    "a c" 'ai-code-code-change
    "a p" 'ai-code-open-prompt-file

    "b n" 'switch-to-next-buffer
    "b p" 'switch-to-prev-buffer
    "b i" 'consult-buffer
    "b b" 'ibuffer
    "b d" 'kill-current-buffer
    "b s" 'save-buffer
    "b l" 'previous-buffer

    "e e" 'neotree-toggle
    "e d" 'dired-jump

    "o a" 'org-agenda
    "o f" 'org-roam-node-find
    "o i" 'org-roam-node-insert

    "g g" 'magit-status
    "g l" 'magit-log-current
    "g d" 'magit-diff-buffer-file
    "g D" 'diff-hl-show-hunk
    "g b" 'vc-annotate

    "h m" 'describe-mode
    "h f" 'describe-function
    "h v" 'describe-variable
    "h k" 'describe-key

    "m p" (lambda ()
            (interactive)
            (shell-command (concat "prettier --write " (shell-quote-argument (buffer-file-name))))
            (revert-buffer t t t))

    "j b" 'consult-project-buffer
    "j o" 'ek/project-switch-project-in-workspace
    "j k" 'project-kill-buffers
    "j n" 'tab-next
    "j p" 'tab-previous
    "j c" 'tab-bar-new-tab
    "j x" 'tab-bar-close-tab
    "j r" 'tab-bar-rename-tab

    "s f" 'consult-find
    "s g" 'consult-grep
    "s G" 'consult-git-grep
    "s r" 'consult-ripgrep
    "s h" 'consult-info

    "t t" 'ek/vterm-toggle-popup
    "t T" 'ek/vterm-open-full

    "w w" 'other-window
    "w h" 'windmove-left
    "w j" 'windmove-down
    "w k" 'windmove-up
    "w l" 'windmove-right
    "w H" 'ek/window-resize-left
    "w J" 'ek/window-resize-down
    "w K" 'ek/window-resize-up
    "w L" 'ek/window-resize-right
    "w s" 'ek/split-window-below-and-focus
    "w v" 'ek/split-window-right-and-focus
    "w d" 'delete-window
    "w o" 'delete-other-windows
    "w u" 'winner-undo
    "w r" 'winner-redo

    "x x" 'consult-flymake
    "x d" 'dired
    "x j" 'dired-jump

    "f f" 'find-file)

  (ek/leader
    :keymaps 'lsp-mode-map
    "c a" 'lsp-execute-code-action
    "r n" 'lsp-rename
    "l f" 'lsp-format-buffer))


;;; EVIL COLLECTION
(use-package evil-collection
  :defer t
  :straight t
  :ensure t
  :custom
  (evil-collection-want-find-usages-bindings t)
  ;; Hook to initialize `evil-collection' when `evil-mode' is activated.
  :hook
  (evil-mode . evil-collection-init))


;;; EVIL SURROUND
(use-package evil-surround
  :ensure t
  :straight t
  :after evil-collection
  :config
  (global-evil-surround-mode 1))


;;; EVIL MATCHIT
(use-package evil-matchit
  :ensure t
  :straight t
  :after evil-collection
  :config
  (global-evil-matchit-mode 1))


;;; UNDO TREE
(use-package undo-tree
  :defer t
  :ensure t
  :straight t
  :hook
  (after-init . global-undo-tree-mode)
  :init
  (setq undo-tree-visualizer-timestamps t
        undo-tree-visualizer-diff t
        undo-tree-auto-save-history nil
        ;; Increase undo limits to avoid losing history due to Emacs' garbage collection.
        ;; These values can be adjusted based on your needs.
        ;; 10X bump of the undo limits to avoid issues with premature
        ;; Emacs GC which truncates the undo history very aggressively.
        undo-limit 800000                     ;; Limit for undo entries.
        undo-strong-limit 12000000            ;; Strong limit for undo entries.
        undo-outer-limit 120000000)           ;; Outer limit for undo entries.
  :config
  ;; Set the directory where `undo-tree' will save its history files.
  ;; This keeps undo history across sessions, stored in a cache directory.
  (setq undo-tree-history-directory-alist '(("." . "~/.emacs.d/.cache/undo"))))


;;; RAINBOW DELIMITERS
(use-package rainbow-delimiters
  :defer t
  :straight t
  :ensure t
  :hook
  (prog-mode . rainbow-delimiters-mode))


;;; DOTENV
(use-package dotenv-mode
  :defer t
  :straight t
  :ensure t
  :config)


;;; PULSAR
(use-package pulsar
  :defer t
  :straight t
  :ensure t
  :hook
  (after-init . pulsar-global-mode)
  :config
  (setq pulsar-pulse t)
  (setq pulsar-delay 0.025)
  (setq pulsar-iterations 10)
  (setq pulsar-face 'evil-ex-lazy-highlight)

  (add-to-list 'pulsar-pulse-functions 'evil-scroll-down)
  (add-to-list 'pulsar-pulse-functions 'flymake-goto-next-error)
  (add-to-list 'pulsar-pulse-functions 'flymake-goto-prev-error)
  (add-to-list 'pulsar-pulse-functions 'evil-yank)
  (add-to-list 'pulsar-pulse-functions 'evil-yank-line)
  (add-to-list 'pulsar-pulse-functions 'evil-delete)
  (add-to-list 'pulsar-pulse-functions 'evil-delete-line)
  (add-to-list 'pulsar-pulse-functions 'evil-jump-item)
  (add-to-list 'pulsar-pulse-functions 'diff-hl-next-hunk)
  (add-to-list 'pulsar-pulse-functions 'diff-hl-previous-hunk))


;;; DOOM MODELINE
(use-package doom-modeline
  :ensure t
  :straight t
  :defer t
  :custom
  (doom-modeline-buffer-file-name-style 'buffer-name)  ;; Set the buffer file name style to just the buffer name (without path).
  (doom-modeline-project-detection nil)                ;; Avoid project resolution on every file open.
  (doom-modeline-buffer-name t)                        ;; Show the buffer name in the mode line.
  (doom-modeline-vcs-max-length 25)                    ;; Limit the version control system (VCS) branch name length to 25 characters.
  :config
  (if ek-use-nerd-fonts                                ;; Check if nerd fonts are being used.
      (setq doom-modeline-icon t)                      ;; Enable icons in the mode line if nerd fonts are used.
    (setq doom-modeline-icon nil))                     ;; Disable icons if nerd fonts are not being used.
  (remove-hook 'find-file-hook #'doom-modeline-update-vcs)
  (remove-hook 'after-save-hook #'doom-modeline-update-vcs)
  (when (advice-member-p #'doom-modeline-update-vcs #'vc-refresh-state)
    (advice-remove #'vc-refresh-state #'doom-modeline-update-vcs))
  :hook
  (after-init . doom-modeline-mode))


;;; NEOTREE
(use-package neotree
  :ensure t
  :straight t
  :custom
  (neo-show-hidden-files t)                ;; By default shows hidden files (toggle with H)
  (neo-theme 'nerd)                        ;; Set the default theme for Neotree to 'nerd' for a visually appealing look.
  (neo-vc-integration '(face char))        ;; Enable VC integration to display file states with faces (color coding) and characters (icons).
  :defer t                                 ;; Load the package only when needed to improve startup time.
  :config
  (if ek-use-nerd-fonts                    ;; Check if nerd fonts are being used.
      (setq neo-theme 'nerd-icons)         ;; Set the theme to 'nerd-icons' if nerd fonts are available.
    (setq neo-theme 'nerd)))               ;; Otherwise, fall back to the 'nerd' theme.


;;; NERD ICONS
(use-package nerd-icons
  :if ek-use-nerd-fonts                   ;; Load the package only if the user has configured to use nerd fonts.
  :ensure t                               ;; Ensure the package is installed.
  :straight t
  :defer t)                               ;; Load the package only when needed to improve startup time.


;;; NERD ICONS Dired
(use-package nerd-icons-dired
  :if ek-use-nerd-fonts                   ;; Load the package only if the user has configured to use nerd fonts.
  :ensure t                               ;; Ensure the package is installed.
  :straight t
  :defer t                                ;; Load the package only when needed to improve startup time.
  :hook
  (dired-mode . nerd-icons-dired-mode))


;;; NERD ICONS COMPLETION
(use-package nerd-icons-completion
  :if ek-use-nerd-fonts                   ;; Load the package only if the user has configured to use nerd fonts.
  :ensure t                               ;; Ensure the package is installed.
  :straight t
  :after (:all nerd-icons marginalia)     ;; Load after `nerd-icons' and `marginalia' to ensure proper integration.
  :config
  (nerd-icons-completion-mode)            ;; Activate nerd icons for completion interfaces.
  (add-hook 'marginalia-mode-hook #'nerd-icons-completion-marginalia-setup)) ;; Setup icons in the marginalia mode for enhanced completion display.


;;; THEMES
(use-package catppuccin-theme
  :ensure t
  :straight t
  :init
  (setq catppuccin-flavor 'mocha)
  :config
  (custom-set-faces
   `(diff-hl-change ((t (:background unspecified :foreground ,(catppuccin-get-color 'blue))))))
  (custom-set-faces
   `(diff-hl-delete ((t (:background unspecified :foreground ,(catppuccin-get-color 'red))))))
  (custom-set-faces
   `(diff-hl-insert ((t (:background unspecified :foreground ,(catppuccin-get-color 'green))))))
  :defer t)

(use-package gruvbox-theme
  :ensure t
  :straight t
  :config
  (load-theme 'gruvbox-dark-medium :no-confirm))


;;; UTILITARY FUNCTION TO INSTALL EMACS-KICK
(defun ek/first-install ()
  "Install tree-sitter grammars and compile packages on first run..."
  (interactive)                                      ;; Allow this function to be called interactively.
  (switch-to-buffer "*Messages*")                    ;; Switch to the *Messages* buffer to display installation messages.
  (message ">>> All required packages installed.")
  (message ">>> Configuring Emacs-Kick...")
  (message ">>> Configuring Tree Sitter parsers...")
  (require 'treesit-auto)
  (treesit-auto-install-all)                         ;; Install all available Tree Sitter grammars.
  (message ">>> Configuring Nerd Fonts...")
  (require 'nerd-icons)
  (nerd-icons-install-fonts)                         ;; Install all available nerd-fonts
  (message ">>> Emacs-Kick installed! Press any key to close the installer and open Emacs normally. First boot will compile some extra stuff :)")
  (read-key)                                         ;; Wait for the user to press any key.
  (kill-emacs))                                      ;; Close Emacs after installation is complete.

(provide 'init)
;;; init.el ends here
